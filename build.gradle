plugins {
    id 'kotlin-multiplatform' version '1.3.21'
    id 'kotlinx-serialization' version '1.3.21'
}
repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url "https://dl.bintray.com/orangy/maven" }
}
group 'dev.sandwwraith'
version '0.0.1'

apply plugin: 'maven-publish'

kotlin {
    jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }
    js()
    // For ARM, should be changed to iosArm32 or iosArm64
    // For Linux, should be changed to e.g. linuxX64
    // For MacOS, should be changed to e.g. macosX64
    // For Windows, should be changed to e.g. mingwX64
    macosX64("macos") {
        binaries {
            executable {
                entryPoint = 'dev.sandwwraith.utils.main'
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.10.0"
                implementation "org.jetbrains.kotlinx:kotlinx-files:0.1.0-dev-24"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.10.0"
                implementation "org.jetbrains.kotlinx:kotlinx-files-jvm:0.1.0-dev-24"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:0.10.0"
                implementation "org.jetbrains.kotlinx:kotlinx-files-js:0.1.0-dev-24"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
        macosMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:0.10.0"
                implementation "org.jetbrains.kotlinx:kotlinx-files-macosx64:0.1.0-dev-24"
            }
        }
        macosTest {
        }
    }
    targets {
        configure([]) {
            tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
                freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
            }
        }
    }
}

task runJvm(dependsOn: 'compileKotlinJvm', type: JavaExec) {
    classpath = kotlin.jvm().compilations["main"].compileDependencyFiles + kotlin.jvm().compilations["main"].runtimeDependencyFiles + kotlin.jvm().compilations["main"].output.allOutputs
    main = 'dev.sandwwraith.utils.PlatformKt'
    standardInput = System.in
}

task runNative(dependsOn: 'linkDebugExecutableMacos', type: Exec) {
    commandLine 'build/bin/macos/debugExecutable/backchair-db.kexe'
    standardInput = System.in
}
